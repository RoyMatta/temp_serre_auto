/*

========================================= Test cours nouvel algo été 2024 =========================

*/

#include <Adafruit_MCP23X17.h>  //SPI
#include <Adafruit_MAX31865.h>  //PT100 température
#include <Wire.h>

#include <WiFi.h>
#include <PubSubClient.h>
#include <ArduinoJson.h>

//Coordonnées de connexion WIFI
const char* ssid = "Roy";
const char* password = "roymatta234567";

//Coordonnées de connexion MQTT
const char* mqttServer = "109.14.127.184";
const int mqttPort = 1883;
const char* mqttUsername = "mqtt-user"; // Remplacer avec le nom d'utilisateur MQTT. 
const char* mqttPassword = "mqtt-password"; // Remplacer avec le mot de passe MQTT.

WiFiClient espClient; //Nous declarons l'esp comme un client du reseau WIFI
PubSubClient client(espClient); //Nous declarons l'esp comme un client du reseau MQTT



// CHIP SELECT pour la communication SPI
#define MODULE_MCP_CS    5  // Module d'entrées/sorties MCP23S17
//#define MODULE_TEMP_A_CS  2  // Amplificateur de sonde de température PT100 MAX31865


Adafruit_MCP23X17 mcp;  //MCP23S17 = GPIO 16 bits expander

// use hardware SPI, just pass in the CS pin
//Adafruit_MAX31865 thermo = Adafruit_MAX31865(2);

//Pin des sorties liées au MCP
#define LED_ON 8
#define LED_DEFAUT 9
#define LED_MARCHE_MOT_A 10
#define LED_MARCHE_MOT_B 11
#define LED_MARCHE_VENTILO 12
#define LIBRE_1 13
#define LIBRE_2 14
#define RELAIS_VENTILO 15

//Pin des boutons liées au MCP
#define BTN_MARCHE_VENTILO 0
#define BTN_MODE_VENTILO 1
#define BTN_PUSH_B_DESC 2
#define BTN_PUSH_B_MONT 3
#define BTN_PUSH_A_DESC 4
#define BTN_PUSH_A_MONT 5
#define BTN_MODE_MOTEUR 6 //6
#define BTN_PUSH_ON 7     //7
#define BTN_ArretUrgence  8                 //TBD correction

#define DIR1 25      //moteur 1
#define PWM1 26

#define DIR2 32      //moteur 2 à vérifier les pins
#define PWM2 33

#define MOTEUR_ARRET 0 //Variable pour arreter le moteur
#define MOTEUR_MONTE 1 //Variable pour ouvrir la serre
#define MOTEUR_DESCEND 2 //Variable pour fermer la serre

#define PIN_BTN_ArretUrgence  22                    //TBD correction
// Capteurs de fin de cours
#define MOTEUR_A_FC_BAS         13 
#define MOTEUR_A_FC_HAUT        12
#define MOTEUR_B_FC_BAS         14
#define MOTEUR_B_FC_HAUT        27

unsigned long TIMEOUT_MOTEURS = 3000; //Durée de fonctionnement d'un cycle du moteur

//Définition de la structure du moteur
typedef struct T_Moteur {
  int pwm;    //pin responsable du pwm
  int dir;    //pin responsable de la direction du moteur
  int etat;    //valuer qui indique l'état du moteur
  unsigned long timestamp; //timestamp responsable de la durée de marche du moteur
  int capteur_FC_haut;  //capteur de fin de course haut
  int capteur_FC_bas;  //capteur de fin de course bas
  int pin_LED_temoin;  //Led qui indique l'état du moteur s'il est en marche ou arret
} T_Moteur ;

T_Moteur lMoteurs[]= 
  {
     { PWM1, DIR1, MOTEUR_ARRET, 0, MOTEUR_A_FC_HAUT, MOTEUR_A_FC_BAS, LED_MARCHE_MOT_A}, //Moteur 1
     { PWM2, DIR2, MOTEUR_ARRET, 0, MOTEUR_B_FC_HAUT, MOTEUR_B_FC_BAS, LED_MARCHE_MOT_B} //Moteur 2
  };

unsigned long ANTIREBOND =30; //Variable pour l'anti-rebond

//Définition de la structure du bouton
typedef struct T_Bouton {
  bool etat; //etat du bouton actuel si actif ou pas à l'instant n
  bool last_etat; //etat précedent du bouton à l'instant n-1
  int pin; //pin du  bouton sur l'esp
  unsigned long timestamp; //timestamp responsable de l'anti-rebond
} T_Bouton;

T_Bouton lBoutons[]=
  {
    {false, false, BTN_MARCHE_VENTILO, 0}, //Bouton de marche des ventilateurs
    {false, false, BTN_MODE_VENTILO, 0}, //Bouton mode automatique ou manuel des ventilateurs
    {false, false, BTN_PUSH_B_DESC, 0}, //Bouton de descente du moteur B
    {false, false, BTN_PUSH_B_MONT, 0}, //Bouton de montée du moteur B
    {false, false, BTN_PUSH_A_DESC, 0}, //Bouton de descente edu moteur A
    {false, false, BTN_PUSH_A_MONT, 0}, //Bouton de montée du moteur A
    {false, false, BTN_MODE_MOTEUR, 0}, //Bouton mode automatique ou manuel des moteurs
    {false, false, BTN_PUSH_ON, 0}, //Bouton de système pour le mettre actif
    {false, false, PIN_BTN_ArretUrgence, 0},    //Bouton arret d'urgence //TBD correction
  }; 


bool syst_on = false; //booléen responsable de l'activation du système
bool mode_manu = false; //booléen responsable de l'activation du mode manuel du système

bool mode_moteur_auto = false;  //booléen responsable de l'activation du mode automatique des moteurs
bool mode_ventilo_auto = false; //booléen responsable de l'activation du mode automatique des ventilateurs

bool etatArretUrgence = false; //booléen responsable de l'état du bouton de l'arret d'urgence
bool etatErreurWifi = false;   //booléen responsable de l'erreur WiFi
bool etatErreurMQTT = false;   //booléen responsable de l'erreur MQTT


int ancien_ouvrant = 0; //valeur de réferenece qui va servir pour comparer de combien il faut ouvrir ou fermer la serre à l'instant n-1
int action_ouvrant = 0; //valeur de combien il faut ouvrir ou fermer à l'instant n
int difference = 0; //différence entre les 2 valeurs pour determiner de combien on ouvre ou on ferme la serre
int rapport_ouverture = 0;
int ouvrant = 0; 
const int humiditySensorPin = 34; //Pin du capteur d'humidité
int valeurCapteur; //valeur de l'humidité donnée par le capteur
int ventilateur = 0; //valeur responsable de l'activation des ventilateurs

//Pour le capteur de température
// Use software SPI: CS, DI, DO, CLK
Adafruit_MAX31865 thermo1 = Adafruit_MAX31865(15, 23, 19, 18);
Adafruit_MAX31865 thermo2 = Adafruit_MAX31865(2, 23, 19, 18);
// use hardware SPI, just pass in the CS pin
//Adafruit_MAX31865 max = Adafruit_MAX31865(10);

// The value of the Rref resistor. Use 430.0 for PT100 and 4300.0 for PT1000
#define RREF      430.0
// The 'nominal' 0-degrees-C resistance of the sensor
// 100.0 for PT100, 1000.0 for PT1000
#define RNOMINAL  100.0

#define WINDOW_SIZE 128
int16_t results[WINDOW_SIZE] = {0}; // TBD DELETE
int current_result = 0;

volatile float Temperature;
volatile float Temperature1;
volatile float Temperature2;
volatile float Moyenne_temp;

//Capteurs humidité
float voltage;
float humidite;
int sensorValue;

//===============================================================================
//------------------------ Fonctions gestion LEDs  ------------------------------
//===============================================================================

//-----------------------------------------------------
/// @brief allume ou eteint la LED
/// @param numero  numero associé à pin de LED
/// @param etat true=allume, false=eteint
void allumeLED(int numero, bool etat) {
  mcp.digitalWrite(numero, etat ? HIGH : LOW);
}

void activeVentilo(bool etat) {
  mcp.digitalWrite(RELAIS_VENTILO, etat);
  allumeLED(LED_MARCHE_VENTILO, etat);
}

unsigned long timestamp_LED = 0;
bool etat_LED = false;

#define DUREE_URGENCE_ON 500
#define DUREE_URGENCE_OFF 500

#define DUREE_WIFI_ON 1000
#define DUREE_WIFI_OFF 500

#define DUREE_MQTT_ON 500
#define DUREE_MQTT_OFF 1000
//-----------------------------------------------------
/// @brief gere clignotement LED DEFAUT
/// @param duree_ON tps allumage
/// @param duree_OFF tps LED eteinte
void check_LED(int duree_ON, int duree_OFF) {
  if(!etat_LED) {
      if(millis()> timestamp_LED + duree_OFF) {
        allumeLED(LED_DEFAUT, true);
        etat_LED = true;
        timestamp_LED = millis();
      }
    } else {
      if(millis()> timestamp_LED + duree_ON) {
        allumeLED(LED_DEFAUT, false);
        etat_LED = false;
        timestamp_LED = millis();
      }
    }
}

/// @brief gère clignotement led DEFAUT en fct de l'ERREUR
void loop_LEDs() {
  //Si on a un arret d'urgence
  if(etatArretUrgence) {
    check_LED(DUREE_URGENCE_ON, DUREE_URGENCE_OFF);   //verifier mise en of du BTN_ON + LED
    return;
  }
  //Si on a une erreur de connexion Wifi
  if(etatErreurWifi) {
    check_LED(DUREE_WIFI_ON, DUREE_WIFI_OFF);

    Serial.println("erreur wifi");
    return;
  } 
  //Si on a une erreur de connexion MQTT
  if(etatErreurMQTT) {
    check_LED(DUREE_MQTT_ON, DUREE_MQTT_OFF);
    Serial.print("erreur mqtt");
    return;
  }
  if(etat_LED) {
    etat_LED = false;                 //si aucune erreur on eteint la LED Defaut
    allumeLED(LED_DEFAUT, false);
  }
}

//===============================================================================
//-------------------------- Fonctions gestion MOTEURS  ---------------------------------
//===============================================================================

/// @brief arrete le moteur grâce à son numéro (0 ou 1)
/// @param numero 
void arretMoteur(int numero) {
  if(lMoteurs[numero].etat == MOTEUR_ARRET) {
    return;
  }
  digitalWrite(lMoteurs[numero].dir, LOW);
  digitalWrite(lMoteurs[numero].pwm, LOW);
  lMoteurs[numero].etat = MOTEUR_ARRET;
  allumeLED(lMoteurs[numero].pin_LED_temoin, false);
}
/// @brief active la montée ou descente du moteur 0 ou 1
/// @param numero 
/// @param etat 
void activeMoteur(int numero, int etat) { //demarrage du moteur
  if(lMoteurs[numero].etat == etat) { //si déjà actif
    return;
  }
  allumeLED(lMoteurs[numero].pin_LED_temoin, true);
  digitalWrite(lMoteurs[numero].pwm, HIGH); 
  lMoteurs[numero].timestamp = millis();
  if(etat == MOTEUR_MONTE) {   //TBD encore plus simple que les TBD suivant .dir !=etat
    digitalWrite(lMoteurs[numero].dir, LOW);    //TBD REMPLACER LOW par sens_montee et définir sens_montée
  } else {
    digitalWrite(lMoteurs[numero].dir, HIGH);   //TBD
  }
  lMoteurs[numero].etat = etat;
}

bool isMoteurActif(int numero) {
  return !(lMoteurs[numero].etat == MOTEUR_ARRET);
}

unsigned long duree_moteur = TIMEOUT_MOTEURS;
bool to_stop = false;
/// @brief désactive un moteur si un critère d'arret est vérifié : timeout, rapport d'ouverture si mode auto activé, capteur FC
/// @param numero 
void check_moteur(int numero) {   //TBD ajouter variation d'un timeout pour mode manu, ajouter argument timeout
  to_stop = false;
  duree_moteur = TIMEOUT_MOTEURS;
  if(mode_moteur_auto) {
    duree_moteur = 0.01*rapport_ouverture*duree_moteur;   ///ajuste duree ouvrant moteur selon pourcentage du rapport d'ouverture
  }
  if(isMoteurActif(numero) && millis()> lMoteurs[numero].timestamp + duree_moteur) { //TBD normaliser gestion erreurs + faire fonction dépassement TIMEOUT général
    to_stop =true;
  }
  if(lMoteurs[numero].etat == MOTEUR_MONTE && digitalRead(lMoteurs[numero].capteur_FC_haut)) { //TBD bool moteur FC capté
    to_stop = true;
  } 
  if(lMoteurs[numero].etat == MOTEUR_DESCEND && digitalRead(lMoteurs[numero].capteur_FC_bas)) { //TBD bool moteur FC capté
    to_stop = true;
  } 
  if(to_stop) {
    arretMoteur(numero);
  }
}
//boucle moteur qui est responsable d'éteindre les moteurs si une condition est vérifiée
void loop_moteurs() {
  check_moteur(0);    //avec timeout
  check_moteur(1);
}


//===============================================================================
//------------------------ Fonctions gestion BOUTONS  ---------------------------
//===============================================================================

/// @brief marche seulement pour les entrées MCP, test état bouton, vérifier qd change s'annule
/// @param numero 
/// @return bool
bool changeEtatBouton(int numero) { 
  int etat;
  if(numero == BTN_ArretUrgence) {
    etat = !digitalRead(lBoutons[numero].pin); //TBD vérifier si pull-up/down
  } else {
    etat = !mcp.digitalRead(lBoutons[numero].pin); //TBD vérifier si pull-up/down
  }
  // Serial.print("ETAT MCP : ");
  // Serial.println(etat);
  //Serial.println(lBoutons[numero].pin);

  int last_etat = lBoutons[numero].last_etat;
  if(etat == last_etat) {
    return false;   
  }

  if(lBoutons[numero].timestamp==0) {
    lBoutons[numero].timestamp = millis();    //TBD serial.print btn déclanché
  }
  if(millis()>lBoutons[numero].timestamp + ANTIREBOND) {      //anti rebond bien dépassé
    lBoutons[numero].etat = etat;          
    lBoutons[numero].last_etat = lBoutons[numero].etat;
    lBoutons[numero].timestamp = 0;
    Serial.print("numero bouton: ");
    Serial.println(numero);
    Serial.print("pin Bouton numero etat apres anti rebond: ");
    Serial.println(lBoutons[numero].etat);
    return true;
  }
  return false;
}

/// @brief s'active une fois l'antirebond dépassé et bouton push ON
/// @param numero 
/// @return bouton pressé ou non
bool boutonPresse(int numero) {
  //Serial.println(lBoutons[numero].etat);
  return (changeEtatBouton(numero) && lBoutons[numero].etat); 
}

/// @brief met a jour l'état des boutons, et detecte les changements d'état, en fonction du type (btn poussoir ou selecteur)
void loop_boutons() {

  if(changeEtatBouton(BTN_ArretUrgence)) { // si arret urgence vient d'etre déclanché //TBD antirebond du btn arret urgence
    etatArretUrgence = !etatArretUrgence;     //test si changement etat
    // Serial.print("Arret urgence : ");
    // Serial.println(syst_on);
    syst_on = false;
    allumeLED(LED_ON, false);
    allumeLED(LED_MARCHE_MOT_A, false);
    allumeLED(LED_MARCHE_MOT_B, false);
    allumeLED(LED_MARCHE_VENTILO, false);
    allumeLED(LED_DEFAUT, false);
    //arretMoteur(0);
    //arretMoteur(1);
                                                          //TBD historiser déclanchement arret d'urgence
  }

  if(!lBoutons[BTN_ArretUrgence].etat && boutonPresse(BTN_PUSH_ON)) { //si remis en marche après arret urgence    //TBD vérifier
    etatArretUrgence= false;
    syst_on = !syst_on;             //avant : syst_on = lBoutons[BTN_PUSH_ON].etat;
    allumeLED(LED_ON, syst_on);
    Serial.print("Système redemarré : ");
    Serial.println(syst_on);
  }
  if(syst_on) {
    if(changeEtatBouton(BTN_MODE_VENTILO)) {
      mode_ventilo_auto = lBoutons[BTN_MODE_VENTILO].etat;
    }
    if(changeEtatBouton(BTN_MODE_MOTEUR)) {       
      mode_moteur_auto = lBoutons[BTN_MODE_MOTEUR].etat;
    }
    if(!mode_moteur_auto) {            //bloc allumé mode manuel
      if(boutonPresse(BTN_PUSH_A_DESC)) {
        if(lMoteurs[0].etat==MOTEUR_DESCEND) {    //moteur déjà en descente, on arrête la descente
          arretMoteur(0);
        }
        else {
          activeMoteur(0,MOTEUR_DESCEND);         //amorçage descente
        }
      }
      if(boutonPresse(BTN_PUSH_A_MONT)) {
        if(lMoteurs[0].etat==MOTEUR_MONTE) {
          arretMoteur(0);
        }
        else {
          activeMoteur(0,MOTEUR_MONTE);
        }
      }

      if(boutonPresse(BTN_PUSH_B_DESC)) {
        if(lMoteurs[1].etat==MOTEUR_DESCEND) {
          arretMoteur(1);
        }
        else {
          activeMoteur(1,MOTEUR_DESCEND);
        }
      }
      if(boutonPresse(BTN_PUSH_B_MONT)) {
        if(lMoteurs[1].etat==MOTEUR_MONTE) {
          arretMoteur(1);
        }
        else {
          activeMoteur(1,MOTEUR_MONTE);
        }
      }
    }
    if(!mode_ventilo_auto) {
      if(changeEtatBouton(BTN_MARCHE_VENTILO)) {        //en mode manuel, active le ventilo en fonction du selecteur BTN_MARCHE_VENTILO
        activeVentilo(lBoutons[BTN_MARCHE_VENTILO].etat);
      }
    }
  }
  else {
    arretMoteur(0);
    arretMoteur(1);
    activeVentilo(LOW);
  }
  // if(etatArretUrgence) {      //si déjà en arret urgence //TBD check si moteurs OFF ?
  //   syst_on = false;             //avant : syst_on = lBoutons[BTN_PUSH_ON].etat;
  //   allumeLED(LED_ON, syst_on);
  //   Serial.print("arret urgence active : ");
  //   Serial.println(syst_on);
  //   return;
  // }
  //version test sans MCP
  // else if(boutonPresse(BTN_PUSH_ON)) {     //else if ?
  //   syst_on = !syst_on;             //avant : syst_on = lBoutons[BTN_PUSH_ON].etat;
  //   allumeLED(LED_ON, syst_on);
  //   Serial.print("===================================================================================== etat systeme");
  //   Serial.println(syst_on);
  // }
  
}

//===============================================================================
//----------------------- Capteur de temperature ---------------------------
//===============================================================================
float LireTemp(){
  int time = millis();
  uint16_t rtd1 = thermo1.readRTD();
  uint16_t rtd2 = thermo2.readRTD();

  Temperature1 = (thermo1.temperature(RNOMINAL, RREF));
  Temperature1 = (Temperature1 - 1.52); //correccion de temperatura
  Temperature2 = (thermo2.temperature(RNOMINAL, RREF));
  Temperature2 = (Temperature2 - 1.52); //correccion de temperatura
  //Moyenne_temp = (Temperature1+Temperature2)/2 ;
  return Temperature1;
}

void EnvoyerTemp(){
  float temperature = LireTemp();
    // Create a JSON object and populate it
  StaticJsonDocument<200> doc;  // Adjust size according to your needs
  doc["temperature"] = temperature;
  
  // Generate the JSON string
  char jsonBuffer[512];  // Adjust size according to your needs
  serializeJson(doc, jsonBuffer);

  // Publish the JSON string to the MQTT topic serre 1
  client.publish("serre1", jsonBuffer);
}


//===============================================================================
//----------------------- Capteur de humidite ---------------------------
//===============================================================================

/// @brief mesure le potentiel: valeur d'humidité
/// @return renvoie valeurCapteur, l'humidité en analogique (0 to 1024)
float LireHum() {
  sensorValue = analogRead(humiditySensorPin);
  voltage = (3.6/2100)*sensorValue;
  humidite = 0.03892*voltage*1000-42.017 ;

  //valeurCapteur = 60; //Forcer l'humidité à 60
  // Lecture de la valeur du capteur
  // valeurCapteur = analogRead(humiditySensorPin);
  // Serial.print("Valeur humidity : ");
  // Serial.println(valeurCapteur);
  return humidite;
  // Ajout d'un léger délai pour éviter les lectures trop rapides
  //delay(1000);
}

/// @brief ajoute sur le serveur MQTT la valeur d'humidité
void EnvoyerHum(){
  float humidite = LireHum();
    // Create a JSON object and populate it
  StaticJsonDocument<200> doc;  // Adjust size according to your needs //TBD changer vers 20-30 (bytes) pour tsous les autres
  doc["humidity"] = humidite;
  
  // Generate the JSON string
  char jsonBuffer[512];  // Adjust size according to your needs
  serializeJson(doc, jsonBuffer);

  // Publish the JSON string to the MQTT topic serre1
  client.publish("serre1", jsonBuffer);
}



//===============================================================================
//---------------------------- Fonctions réseau  ---------------------------------
//===============================================================================
//-------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------

/// @brief met à jour la valeur d'ouverture des volets (=0 si déjà valeur déjà reçue)
/// @return coefficient d'ouverture, multiplié ensuite par un TIMEOUT
int modeAutoMoteurs() {
  Serial.println("---MODE AUTOMATIQUE MOTEURS---");
  //delay(100);
  difference = ouvrant - ancien_ouvrant;
  Serial.print("difference : ");
  Serial.println(difference);
 
  ancien_ouvrant = ouvrant; 
  return difference;
}

//Reconnection to WIFI:
#define TIMEOUT_MAX_CONNECTIONS 5000
unsigned long lastWifiConnexions = 0;
unsigned long lastMQTTConnexions = 0;
bool reconnectWiFi() {
  // Attempt to reconnect to WiFi
  if ( millis() > lastWifiConnexions + TIMEOUT_MAX_CONNECTIONS) {  //retente si delai dépassé
    Serial.println("Connecting to WiFi...");
    WiFi.begin(ssid, password);
    Serial.print(" trying wifi...");
    lastWifiConnexions = millis();       
      
  }
  return WiFi.status() == WL_CONNECTED;
}

/// @brief Reconnection to MQTT ssi delai dépassé
/// @return connecté au MQTT ?
bool reconnectMQTT() {
  if ( millis() > lastMQTTConnexions + TIMEOUT_MAX_CONNECTIONS) {  // Wait 5 seconds before retrying
    // Loop until we're reconnected to MQTT
    Serial.println("Connecting to MQTT...");
    // Attempt to connect
    if (client.connect("ESP32Client", mqttUsername, mqttPassword)) {
      Serial.println("Connected to MQTT");
      // Subscribe to the topic here again because the subscription is lost on disconnection
      client.subscribe("/Moteurs/Serre1");
      //client.subscribe("/Ventilateurs/Serre1");
    } else {
      Serial.print("Failed to connect to MQTT, rc=");
      Serial.print(client.state());
      Serial.println(" try again in 5 seconds");
    }
    lastMQTTConnexions = millis();       
  }
  return client.connected();
}

/// @brief verifie connexion Wifi + MQTT ssi systeme allumé et un des mode auto activé (moteur ou ventilo)
void loop_check_reseau() {                                       //TBD : verifier si bien appelé
  if(syst_on && (mode_ventilo_auto || mode_moteur_auto)) {      //ssi bloc en marche et un des modes en auto
    if (WiFi.status() != WL_CONNECTED) {           //gere la reconnexion WIFI
      if(!reconnectWiFi()) {
        etatErreurWifi = true;
        return;
      } else {
        etatErreurWifi = false;
        Serial.println("================== Wifi connecte !==============");
      } 
    } else {
      Serial.println("Test wifi passe");
      etatErreurWifi = false;             // met a jour l'état d'erreur wifi peut importe l'etat du MQTT
    }
    // Check MQTT connection
    if (!client.connected()) {
     if(!reconnectMQTT()){
        etatErreurMQTT = true;
        return;
      }else{
        etatErreurMQTT = false;
        Serial.println("================== MQTT connecte !==============");
      }
    } 
    Serial.println("Test MQTT passe");
    client.loop();                                //pas sûr d'ici
    EnvoyerTemp();                            //envoie des données vers server
    EnvoyerHum();
    if(mode_moteur_auto) {
        int temp_rapport = modeAutoMoteurs();
        if(temp_rapport !=0) {
          rapport_ouverture = temp_rapport;   //màj des qu'une nouvelle valeur d'ouvrant est lancée.

          if(rapport_ouverture>0) {                   //activation des moteurs dès que nouvelle valeur reçue (1 seule fois)
            activeMoteur(0, MOTEUR_MONTE);            //TBD vérif pourçentage ouverture ca réel
            activeMoteur(1, MOTEUR_MONTE);        
          }
          if(rapport_ouverture<0) {
              activeMoteur(0, MOTEUR_DESCEND);
              activeMoteur(1, MOTEUR_DESCEND);
              rapport_ouverture = - rapport_ouverture;
          }
          }
        //rapport_ouverture = modeAutoMoteurs();       //donne int rapport d'ouverture positive ou negatif

    }
    if(mode_ventilo_auto) {
        //TBD activation ou non tout ou rien
    }
  }
  else{
    etatErreurMQTT = false;
    etatErreurWifi = false;
  }
}

// void loop_reseau() {
//   //client.loop();
//   loop_check_reseau();
// }

//===============================================================================
//---------------------------- Fonctions DEBUG  ---------------------------------
//===============================================================================
//#define DebugGPIO 1;   //décommenter pour activer la lecture
//#define DebugCapteursFC 1;
void debugGPIO() {
  for(int i=0; i<8; i++) {
    Serial.print("Etat de la pin : ");
    Serial.print(i);
    Serial.print(" --> ");
     // LOW = pressed, HIGH = not pressed
    Serial.println(!mcp.digitalRead(i));
  }
}

void debugCapteursFC() {
  Serial.printf("Etat capteur FC haut moteur A : %d \n", digitalRead(MOTEUR_A_FC_HAUT));
  Serial.printf("Etat capteur FC bas moteur A : %d \n", digitalRead(MOTEUR_A_FC_BAS));
  Serial.printf("Etat capteur FC haut moteur B : %d \n", digitalRead(MOTEUR_B_FC_HAUT));
  Serial.printf("Etat capteur FC bas moteur B : %d \n", digitalRead(MOTEUR_B_FC_BAS));
  delay(500);
}


//===============================================================================
//--------------------------------- CALLBACK --------------------------------------- 
//===============================================================================

// Callback function that is called when a new message arrives at the subscribed topic

/// @brief Callback: fonction appellee lors de la reception d'un message(commande des moteurs ou ventilateurs) pour traiter le message recu.
void callback(char* topic, byte* payload, unsigned int length) {
  Serial.print("Message arrived in topic: ");
  Serial.println(topic); //affichage du topic dans lequel on a recu le message
  
  // Convertir le message en chaine de caracteres
  String message = "";
  for (int i = 0; i < length; i++) {
    message += (char)payload[i];
  }
  Serial.print("Message: ");
  Serial.println(message);

   // Vérifier si le message reçu est celui responsable de la fermeture ou ouverture de la serre.
  if (strcmp(topic, "/Moteurs/Serre1") == 0) {
    // Process the message for /Moteurs/Serre1 topic
    //Permet de convertir le message qui est sous format JSON en forme de chaine de caracteres afin de recuperer les valeurs.
    // Parse JSON message
    StaticJsonDocument<200> doc;
    DeserializationError error = deserializeJson(doc, message);
    // Check for errors in parsing
    if (error) {
      //Serial.print("deserializeJson() failed: ");
      //Serial.println(error.c_str());
      return;
    }
    // Extract the value of "ouvrant"
    ouvrant = doc["ouvrant"];
    Serial.printf("Ouvrant lu ds callback = %d\n", ouvrant);
  }

    // Vérifier si le message reçu est celui responsable de l'activation des ventilateurs.
  else if (strcmp(topic, "/Ventilateurs/Serre1") == 0) {
    // Process the message for /Ventilateurs/Serre1 topic
    StaticJsonDocument<200> doc;
    DeserializationError error = deserializeJson(doc, message);

    if (error) {
      Serial.print("deserializeJson() failed: ");
      Serial.println(error.c_str());
      return;
    }

    // Extract the value for ventilateur (assumed key in JSON message)
    ventilateur = doc["ventilateur"]; 
    Serial.printf("Ventilateur lu ds callback = %d\n", ventilateur);
  }
}

//===============================================================================
//--------------------------------- SETUP --------------------------------------- 
//===============================================================================
void setup() {
  Serial.begin(115200);
  WiFi.begin(ssid, password); //Connexion au WIFI
    //Connexion au MQTT
  client.setServer(mqttServer, mqttPort);
  client.setCallback(callback); // Set the callback function
  thermo1.begin(MAX31865_3WIRE);
  thermo2.begin(MAX31865_3WIRE);
  /*
  //============================= Partie MQTT ===================================
  WiFi.begin(ssid, password); //Connexion au WIFI
  thermo.begin(MAX31865_3WIRE);
  //Si connexion WIFI echoué 
  while (WiFi.status() != WL_CONNECTED) {
    delay(500);
    Serial.println("Connecting to WiFi..");
  }
  
  //Message de confirmation de connexion en affichant l'addresse IP.
  Serial.println("WiFi connected successfully!");
  Serial.print("IP Address: ");
  Serial.println(WiFi.localIP()); // This line prints the IP address assigned to your ESP32

  //Connexion au MQTT
  client.setServer(mqttServer, mqttPort);
  client.setCallback(callback); // Set the callback function

   //Si connexion MQTT echoue 
  while (!client.connected()) {
    Serial.println("Connecting to MQTT...");

    if (client.connect("ESP32Client", mqttUsername, mqttPassword)) {
      Serial.println("Connected");  
      client.subscribe("/Moteurs/Serre1"); //Topic qui permet de lire les valeurs pour commander l'ouverture des moteurs, recu depuis Home Assistant.
      //client.subscribe("/Ventilateurs/Serre1"); //Topic qui permet de lire les valeurs pour controler la ventilation, recu depuis Home Assistant.
    } else {
      Serial.print("failed with state ");
      Serial.print(client.state());
      delay(2000);
    }
  }

  */
  //=============================================================================

  //=================== Initialisation Expander MCP23S17 ========================
  Serial.println("MCP23xxx Blink Test!");
  if (!mcp.begin_SPI(MODULE_MCP_CS)) {
    Serial.println("Error mcp begin.");
    while (1);
  }

  // configure pin for output
  //mcp.pinMode(LED_PIN, OUTPUT);
  for (int i = 8; i<16; i++) {    //configure en sortie le registre B
    mcp.pinMode(i, OUTPUT);                                 //TBD correction
    mcp.digitalWrite(i, HIGH);    //attention aux sorties MOTEURS ! (B2=10 et B3=11 ?)
    delay(400);
    mcp.digitalWrite(i, LOW);   
  }

  for(int i=0; i<8; i++) {
    mcp.pinMode(lBoutons[i].pin, INPUT_PULLUP);               //TBD correction
    Serial.print("Etat de la pin : ");
    Serial.print(i);
    Serial.print(" --> ");
     // LOW = pressed, HIGH = not pressed
    Serial.println(!mcp.digitalRead(i));
  }
  //=============================================================================

  pinMode(lBoutons[BTN_ArretUrgence].pin, INPUT_PULLUP);

  pinMode(MOTEUR_A_FC_BAS, INPUT_PULLDOWN); //car branchement sur 3.3V vers entrée à potentiel flottant
  pinMode(MOTEUR_A_FC_HAUT, INPUT_PULLDOWN);
  pinMode(MOTEUR_B_FC_BAS, INPUT_PULLDOWN);
  pinMode(MOTEUR_B_FC_HAUT, INPUT_PULLDOWN);

  pinMode(DIR1, OUTPUT);
  pinMode(PWM1, OUTPUT);
  pinMode(DIR2, OUTPUT);
  pinMode(PWM2, OUTPUT);
  // Arrête le moteur pendant 1 seconde
  digitalWrite(DIR1, LOW);
  digitalWrite(PWM1, LOW);
  digitalWrite(DIR2, LOW);
  digitalWrite(PWM2, LOW);
  //TBD lire valeur des commutateurs de mode
  //Lecture des valeurs de commutateurs de mode
  Serial.println("Looping...");

}

void loop() {
  loop_boutons();
  loop_check_reseau();
  loop_LEDs();
  loop_moteurs();
  //TBD boucle envoyer param, timestamp

  #ifdef DebugGPIO
  debugGPIO();
  #endif

  #ifdef DebugCapteursFC
  debugCapteursFC();
  #endif
}